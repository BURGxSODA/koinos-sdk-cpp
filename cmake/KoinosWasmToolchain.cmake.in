set(CMAKE_SYSTEM_NAME Generic)
set(CMAKE_SYSTEM_PROCESSOR wasm)

set(KOINOS_WASI_SDK_ROOT $ENV{KOINOS_WASI_SDK_ROOT} CACHE FILEPATH "The root directory of the WASI SDK")
set(KOINOS_CDT_ROOT $ENV{KOINOS_CDT_ROOT} CACHE FILEPATH "The root directory of the Koinos Contract Development Toolkit")
set(KOINOS_PROTOBUF_ROOT $ENV{KOINOS_PROTOBUF_ROOT} CACHE FILEPATH "The root of the Protocol Buffer installation")
set(KOINOS_EMBEDDED_PROTO_ROOT $ENV{KOINOS_EMBEDDED_PROTO_ROOT} CACHE FILEPATH "The root of the Embedded Proto repository")

if(WIN32)
  set(KOINOS_WASI_HOST_EXE_SUFFIX ".exe")
else()
  set(KOINOS_WASI_HOST_EXE_SUFFIX "")
endif()

set(CMAKE_C_COMPILER ${KOINOS_WASI_SDK_ROOT}/bin/clang${KOINOS_WASI_HOST_EXE_SUFFIX})
set(CMAKE_C_COMPILER_TARGET wasm32-wasi)

set(CMAKE_CXX_COMPILER ${KOINOS_WASI_SDK_ROOT}/bin/clang++${KOINOS_WASI_HOST_EXE_SUFFIX})
set(CMAKE_CXX_COMPILER_TARGET wasm32-wasi)

set(CMAKE_ASM_COMPILER ${KOINOS_WASI_SDK_ROOT}/bin/clang${KOINOS_WASI_HOST_EXE_SUFFIX})

set(CMAKE_AR ${KOINOS_WASI_SDK_ROOT}/bin/llvm-ar${KOINOS_WASI_HOST_EXE_SUFFIX})
set(CMAKE_RANLIB ${KOINOS_WASI_SDK_ROOT}/bin/llvm-ranlib${KOINOS_WASI_HOST_EXE_SUFFIX})
set(CMAKE_LINKER ${KOINOS_WASI_SDK_ROOT}/bin/wasm-ld${KOINOS_WASI_HOST_EXE_SUFFIX})

set(CMAKE_SYSROOT ${KOINOS_WASI_SDK_ROOT}/share/wasi-sysroot)

list(APPEND CMAKE_MODULE_PATH "${KOINOS_CDT_ROOT}/cmake")

set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_CXX_EXTENSIONS OFF)
set(CMAKE_CXX_FLAGS_RELEASE "-O3")

add_compile_options(
  -flto
  -I${KOINOS_WASI_SDK_ROOT}/share/wasi-sysroot/include
  -I${KOINOS_WASI_SDK_ROOT}/share/wasi-sysroot/include/c++/v1
  -I${KOINOS_CDT_ROOT}/include)

add_link_options(
  --no-entry
  --allow-undefined
  --strip-all
  -m wasm32
  ${KOINOS_WASI_SDK_ROOT}/share/wasi-sysroot/lib/wasm32-wasi/crt1.o
  -L${KOINOS_WASI_SDK_ROOT}/share/wasi-sysroot/lib/wasm32-wasi
  -L${KOINOS_WASI_SDK_ROOT}/lib/clang/11.0.0/lib/wasi
  -L${KOINOS_CDT_ROOT}/lib)

set(CMAKE_C_LINK_EXECUTABLE "<CMAKE_LINKER> <LINK_FLAGS> <OBJECTS> -o <TARGET> <LINK_LIBRARIES>")
set(CMAKE_CXX_LINK_EXECUTABLE "<CMAKE_LINKER> <LINK_FLAGS> <OBJECTS> -o <TARGET> <LINK_LIBRARIES>")

set(CMAKE_EXECUTABLE_SUFFIX_C ".wasm")
set(CMAKE_EXECUTABLE_SUFFIX_CXX ".wasm")

set(CMAKE_FIND_ROOT_PATH ${KOINOS_CDT_ROOT})
set(CMAKE_FIND_ROOT_PATH_MODE_PROGRAM NEVER)
set(CMAKE_FIND_ROOT_PATH_MODE_LIBRARY ONLY)
set(CMAKE_FIND_ROOT_PATH_MODE_INCLUDE ONLY)
set(CMAKE_FIND_ROOT_PATH_MODE_PACKAGE ONLY)

